prototype(GSL.DuttweilerDe:Api.Json) < prototype(Neos.Fusion:Http.Message) {
  httpResponseHead {
    headers.Content-Type = 'application/json;charset=utf-8'
  }
  
  content = Neos.Fusion:Case {
    news {
      condition = ${request.arguments.endpoint == 'news'}
      type = 'GSL.DuttweilerDe:Api.Json.News'
    }
    events {
      condition = ${request.arguments.endpoint == 'events'}
      type = 'GSL.DuttweilerDe:Api.Json.Events'
    }
  }
}

prototype(GSL.DuttweilerDe:Api.Json.Object) < prototype(Neos.Fusion:Component) {
  @process.1 = ${Json.stringify(value)}
}

prototype(GSL.DuttweilerDe:Api.Json.News) < prototype(GSL.DuttweilerDe:Api.Json.Object) {
  @context.chronikNode = ${q(site).find('#529a59e2-1849-2782-471c-7635f47167de').get()}
  @context.standaloneItems = ${q(site).find('/sites/duttweilerde').children('standalonenews').children().get()}
  renderer = Neos.Fusion:RawCollection {
    collection = ${q(chronikNode).children('[instanceof GSL.DuttweilerDe.Pages:ChronikBranch]').first().children('[instanceof GSL.DuttweilerDe.Pages:ChronikItem]').slice(0,10).orderByDate().get()}
    itemName = 'item'
    itemRenderer = Neos.Fusion:RawArray {
      uid = ${q(item).property('_name')}
      headline = ${q(item).property('title')}
      subheadline = ${q(item).property('subheadline')}
      date = ${Date.format(q(item).property('date'), 'd.m.Y')}
      text = ${String.stripTags(q(item).children('[instanceof Neos.Neos:ContentCollection]').first().children().first().property('text'))}
      image = Neos.Neos:ImageUri {
        asset = ${q(item).children('[instanceof Neos.Neos:ContentCollection]').first().children().first().property('image')}
      }
      imageAlternativeText = ${q(item).children('[instanceof Neos.Neos:ContentCollection]').first().children().first().property('alternativeText')}
      url = Neos.Neos:NodeUri {
        absolute = ${false}
        format = 'html'
        node = ${q(item).get(0)}
      }
    }
  }
  
  @cache {
    mode = 'cached'
    entryTags {
      1 = ${'NodeType_GSL.DuttweilerDe.Pages:ChronikItem'}
      2 = ${'DescendantOf_' + chronikNode.identifier}
      3 = ${'NodeType_GSL.DuttweilerDe.Content:FrontpageNewsItemStandalone'}
    }
  }
}
